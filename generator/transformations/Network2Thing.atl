-- @atlcompiler emftvm
-- @nsURI ThingML=http://www.thingml.org/xtext/ThingML
-- @nsURI CyprIoT=http://www.atlanmod.org/CyprIoT


module Network2Thing;

create OUT: ThingML from TH: ThingML, CY : CyprIoT;

uses Copier;

helper def : id : Integer = 0;
helper def : position : Integer = 0;

rule createExternalConnectorFromBind {
	from s : CyprIoT!Bind((ThingML!Thing.allInstances().first().name=s.bindsInstanceThing.typeThing.thingToInstantiate.name))
	using {
		protocolName : String = s.channelToBind.oclAsType(CyprIoT!ToBindPubSub).targetedPubSubInstance.typePubSub.targetedProtocol.toString().removeHash();
	}
	to 
	c : ThingML!Configuration(
		name <- s.bindsInstanceThing.typeThing.thingToInstantiate.name+'_Cfg',
		instances <- Sequence{instance},
		connectors <- Sequence{externalConnector},
		annotations <- Sequence{compiler}
	),
	compiler : ThingML!PlatformAnnotation(
		name <- 'compiler',
		value <- s.bindsInstanceThing.typeThing.targetedPlatform.toString().removeHash().toLower()
	),
	instance : ThingML!Instance(
		name <- s.bindsInstanceThing.name,
		type <- ThingML!Thing.allInstances().first() -- TODO : more generic
	),
	externalConnector : ThingML!ExternalConnector(
		inst <- instance,
		port <- ThingML!Port.allInstances()->select(p | p.name=s.portToBind.name).first(),
		protocol <- protocol,
		annotations <- s.getAllTopicsFromBind()->collect(t | thisModule.multipleTopics(s, t))
	),
	protocol : ThingML!Protocol (
		name <- protocolName,
		annotations <- Sequence{brokerAdress,portNumber,serializer}
	),
	brokerAdress : ThingML!PlatformAnnotation(
		name <- 'mqtt_broker_address',
		value <- s.channelToBind.oclAsType(CyprIoT!ToBindPubSub).targetedPubSubInstance.typePubSub.server.toString().split(':').first()
	),
	portNumber : ThingML!PlatformAnnotation(
		name <- 'mqtt_port_number',
		value <- s.channelToBind.oclAsType(CyprIoT!ToBindPubSub).targetedPubSubInstance.typePubSub.server.toString().split(':').last()
	),
	serializer : ThingML!PlatformAnnotation(
		name <- 'serializer',
		value <- s.channelToBind.oclAsType(CyprIoT!ToBindPubSub).topics.first().serializer.toString().removeHash().toLower()
	)
	do {
		if(thisModule.id>=1){
			protocol.name <- protocolName+thisModule.id.toString();
		}
		thisModule.id <- thisModule.id + 1;
	}
}

lazy rule multipleTopics {
	from s : CyprIoT!Bind, t : CyprIoT!Topic
	to annotationMqtt : ThingML!PlatformAnnotation(
		name <- s.bindAction.toString().removeHash().transformArrowToMQTTSyntax,
		value <- s.getTopicPathName(t)
	)
}