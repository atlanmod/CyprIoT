-- @atlcompiler emftvm
-- @nsURI ThingML=http://www.thingml.org/xtext/ThingML
-- @nsURI CyprIoT=http://www.atlanmod.org/CyprIoT


module RuleTrigger;

create OUT: ThingML from TH: ThingML, CY : CyprIoT;

uses Copier;
uses Helpers;

-- Get the network to make (support for first network only for the moment)
helper def : firstNetwork() : CyprIoT!Network = CyprIoT!Network.allInstances().first();

helper context CyprIoT!Network def : collectEnforcedPoliciesInNetwork() : Sequence(CyprIoT!Policy) = 
	self.hasPolicyEnforcement.hasEnforcedPolicies;

helper context CyprIoT!Policy def : collectRuleTriggerFromPolicy() : Sequence(CyprIoT!RuleTrigger) =
	self.hasRules->select(r | r.oclIsTypeOf(CyprIoT!RuleTrigger))
;

helper def : bindOfInputThing() : CyprIoT!Bind = thisModule.firstNetwork().bindsContainingThingInNetwork().first();

helper context CyprIoT!Network def : bindsContainingThingInNetwork() : Sequence(CyprIoT!Bind) = 
	self.hasBinds->select(b | b.isBindMatchesInputThing())
;

-- Clean helpers

helper context CyprIoT!Network def : collectEnforcedEnforcedTriggerRulesInNetwork() : Sequence(CyprIoT!RuleTrigger) = 
	self.collectEnforcedPoliciesInNetwork()->collect(p | p.collectRuleTriggerFromPolicy())->flatten()
;

helper def : collectTriggerRulesInMyNetwork() : Sequence(CyprIoT!RuleTrigger) = 
	thisModule.firstNetwork().collectEnforcedEnforcedTriggerRulesInNetwork()
;

helper context CyprIoT!RuleTrigger def : isInputThingInRuleTriggerFunctionObject() : Boolean = self.effectTrigger.actionTrigger.thingWithFunction.thing.name=thisModule.bindOfInputThing().bindsInstanceThing.typeThing.thingToInstantiate.name;


helper context CyprIoT!RuleTrigger def : isInputThingInRuleTriggerObject() : Boolean = self.effectTrigger.actionTrigger.thingWithState.thing.name=thisModule.bindOfInputThing().bindsInstanceThing.typeThing.thingToInstantiate.name;

helper context CyprIoT!RuleTrigger def : isInputThingInRuleTriggerSubject() : Boolean = self.thingWithState.thing.name=thisModule.bindOfInputThing().bindsInstanceThing.typeThing.thingToInstantiate.name;

helper context ThingML!State def : isStateNameEqualToRuleTriggerObjectState(k : CyprIoT!RuleTrigger) : Boolean = self.name=k.effectTrigger.actionTrigger.thingWithState.getState.state.name;

helper context ThingML!State def : isStateNameEqualToRuleTriggerSubjectState(k : CyprIoT!RuleTrigger) : Boolean = self.name=k.thingWithState.getState.state.name;

helper context CyprIoT!RuleTrigger def : getFunctionToExecute() : ThingML!Function = ThingML!Function.allInstances()->select(f | f.name=self.effectTrigger.actionTrigger.thingWithFunction.getFunction.function.name).first();

helper context CyprIoT!RuleTrigger def : isGoToState() : Boolean = not(self.effectTrigger.actionTrigger.goToState.oclIsUndefined());

helper context CyprIoT!RuleTrigger def : isExecuteFunction() : Boolean = not(self.effectTrigger.actionTrigger.executeFunction.oclIsUndefined());

helper context CyprIoT!RuleTrigger def : isPerformTransition() : Boolean = not(self.effectTrigger.actionTrigger.performTransition.oclIsUndefined());

helper context CyprIoT!RuleTrigger def : triggerObjectStateName() : String = self.effectTrigger.actionTrigger.thingWithState.getState.state.name;

helper context CyprIoT!RuleTrigger def : triggerObjectFunctionName() : String = self.effectTrigger.actionTrigger.thingWithFunction.getFunction.function.name;


rule copyThingMLModel {
	from s : ThingML!ThingMLModel
	to t : ThingML!ThingMLModel(
		configs <- s.configs,
		imports <- s.imports,
		protocols <- s.protocols,
		types <- s.types
	)	
}


rule copyCompositeState {
	from s : ThingML!CompositeState
	to t : ThingML!CompositeState(
		name <- s.name,
		annotations <- s.annotations,
		entry <- s.entry,
		exit <- s.exit,
		history <- s.history,
		initial <- s.initial,
		internal <- s.internal,
		outgoing <- s.outgoing,
		properties <- s.properties,
		region <- s.region,
		session <- s.session,
		substate <- s.substate
	)
}

rule copyExternalConnector {
	from s : ThingML!ExternalConnector
	to t : ThingML!ExternalConnector(
		annotations <- s.annotations,
		inst <- s.inst,
		name <- s.name,
		port <- s.port,
		protocol <- s.protocol
	)
}

rule copyThing {
	from s : ThingML!Thing
	to t : ThingML!Thing(
		name <- s.name,
		ports <- s.ports,
		annotations <- s.annotations,
		assign <- s.assign,
		behaviour <- s.behaviour,
		fragment <- s.fragment,
		functions <- s.functions,
		messages <- s.messages->union(CyprIoT!RuleTrigger.allInstances()),
		includes <- s.includes,
		properties <- s.properties
	)
}

rule copyProvidedPort {
	from s : ThingML!ProvidedPort
	to t : ThingML!ProvidedPort(
		name <- s.name,
		receives <- s.receives->union(CyprIoT!RuleTrigger.allInstances()),
		sends <- s.sends->union(CyprIoT!RuleTrigger.allInstances()),
		annotations <- s.annotations
	)
}

rule copyRequiredPort {
	from s : ThingML!RequiredPort
	to t : ThingML!RequiredPort(
		name <- s.name,
		receives <- s.setReceivesPort(),
		sends <- s.setSendsPort(),
		annotations <- s.annotations
	)
}

helper context ThingML!RequiredPort def : setReceivesPort() : Sequence(ThingML!Message) = 
thisModule.collectTriggerRulesInMyNetwork()->iterate(r ;  receives : Sequence(ThingML!Message) = self.receives |
	if(r.isGoToState() and r.isInputThingInRuleTriggerObject()) 
				then receives->union(CyprIoT!RuleTrigger.allInstances()) else receives endif
			)
;

helper context ThingML!RequiredPort def : setSendsPort() : Sequence(ThingML!Message) = 
thisModule.collectTriggerRulesInMyNetwork()->iterate(r ;  sends : Sequence(ThingML!Message) = self.sends |
	if(r.isGoToState() and r.isInputThingInRuleTriggerObject()) 
		then sends else sends->union(CyprIoT!RuleTrigger.allInstances()) endif
)
;

helper context ThingML!State def : setInternals() : Sequence(ThingML!InternalTransition) = 
	thisModule.collectTriggerRulesInMyNetwork()->iterate(r ;  internals : Sequence(ThingML!InternalTransition) = self.internal |
		if(r.isExecuteFunction() 
				and r.isInputThingInRuleTriggerFunctionObject()) 
					then internals->union(Sequence{thisModule.getInternalTransition(self,r)}) 
					else internals endif	
	)
;
helper context ThingML!State def : setOutgoing() : Sequence(ThingML!Transition) = 
	thisModule.collectTriggerRulesInMyNetwork()->iterate(r ;  transitions : Sequence(ThingML!Transition) = self.outgoing |
	
if(r.isGoToState() and 
	r.isInputThingInRuleTriggerObject() and 
	not(self.isStateNameEqualToRuleTriggerObjectState(r)))
		then if(transitions->exists(tr | tr.guard.oclIsUndefined() and tr.event.oclIsUndefined())) then transitions else transitions->union(Sequence{thisModule.getTransition(self,r)}) endif
	else if(r.isPerformTransition() and not(r.isInputThingInRuleTriggerSubject())) then
	transitions->union(if(ThingML!Transition.allInstances()->select(i | self.name=i.refImmediateComposite().oclAsType(ThingML!State).name)->collect(a | thisModule.multipleTransition(a,r)).asOrderedSet().size()>0 and r.effectTrigger.actionTrigger.transitionRank=0) 
		then 
		Sequence{ThingML!Transition.allInstances()->select(i | self.name=i.refImmediateComposite().oclAsType(ThingML!State).name)->collect(a | thisModule.multipleTransition(a,r)).asOrderedSet().at(1)} 
		else if(ThingML!Transition.allInstances()->select(i | self.name=i.refImmediateComposite().oclAsType(ThingML!State).name)->collect(a | thisModule.multipleTransition(a,r)).asOrderedSet().size()>0 and not(r.effectTrigger.actionTrigger.transitionRank=0) and ThingML!Transition.allInstances()->select(i | self.name=i.refImmediateComposite().oclAsType(ThingML!State).name)->collect(a | thisModule.multipleTransition(a,r)).asOrderedSet().size()<=r.effectTrigger.actionTrigger.transitionRank) 
		then 
		Sequence{ThingML!Transition.allInstances()->select(i | self.name=i.refImmediateComposite().oclAsType(ThingML!State).name)->collect(a | thisModule.multipleTransition(a,r)).asOrderedSet().at(r.effectTrigger.actionTrigger.transitionRank)} 
		else Sequence{} endif endif
	)
	else transitions endif endif
	)
;

helper context ThingML!State def : setOnEntry() : ThingML!Action = 
thisModule.collectTriggerRulesInMyNetwork()->iterate(r ;  action : ThingML!Action = self.entry |
	if(r.isInputThingInRuleTriggerSubject() and self.isStateNameEqualToRuleTriggerSubjectState(r)) 
		then thisModule.groupActionOnEntry(action, r) 
		else action endif
	)
;

helper context CyprIoT!RuleTrigger def : setTransitionTarget() : ThingML!State = 

if(self.isGoToState() 
		and self.isInputThingInRuleTriggerObject()) 
	then ThingML!State.allInstances()->select(t | t.isStateNameEqualToRuleTriggerObjectState(self)).first() 
	else ThingML!State.allInstances().first() endif
;
rule triggerMess {
	from s : CyprIoT!RuleTrigger
	to triggerMessage : ThingML!Message(
		name <- if(s.isGoToState()) 
					then s.triggerObjectStateName()
					else if(s.isExecuteFunction()) 
						then s.triggerObjectFunctionName() 
					else 'perfomTransition'
					endif endif
	)	
}

rule copyState {
	from s : ThingML!State( not(s.oclIsTypeOf(ThingML!CompositeState) or s.oclIsTypeOf(ThingML!FinalState)))
	to 
	t : ThingML!State(
		annotations <- s.annotations,
		entry <- s.setOnEntry(),
		exit <- s.exit,
		internal <- s.setInternals(),
		name <- s.name,
		outgoing <- s.setOutgoing(),
		properties <- s.properties
	)
}

lazy rule getTransition {
	from s : ThingML!State , k : CyprIoT!RuleTrigger
	to 
	transition : ThingML!Transition(
		target <- k.setTransitionTarget(),
		event <- receive
	),
	receive : ThingML!ReceiveMessage(
		port <- ThingML!Thing.allInstances().first().ports.first(),
		message <- thisModule.resolveTemp(k, 'triggerMessage'),
		name <- 'trigger'
	),
	action2 : ThingML!FunctionCallStatement(
		function <- if(not(k.isInputThingInRuleTriggerSubject()) and k.isExecuteFunction()) then k.getFunctionToExecute() else OclUndefined endif,
		parameters <- if(k.isExecuteFunction() and not(k.effectTrigger.actionTrigger.thingWithFunction.getFunction.parameters.oclIsUndefined())) then k.effectTrigger.actionTrigger.thingWithFunction.getFunction.parameters->collect( p | thisModule.multipleParameters(p))
						else Sequence{} endif
	)
}

lazy rule getInternalTransition {
	from s : ThingML!State , k : CyprIoT!RuleTrigger
	to 
	transitionInternal : ThingML!InternalTransition(
		event <- receive2,
		action <- action
	),
	receive2 : ThingML!ReceiveMessage(
		port <- ThingML!Thing.allInstances().first().ports.first(),
		message <- thisModule.resolveTemp(k, 'triggerMessage'),
		name <- 'trigger'
	),
	action : ThingML!FunctionCallStatement(
		function <- if(not(k.isInputThingInRuleTriggerSubject()) and k.isExecuteFunction()) then k.getFunctionToExecute() else OclUndefined endif,
		parameters <- if(k.isExecuteFunction() and not(k.effectTrigger.actionTrigger.thingWithFunction.getFunction.parameters.oclIsUndefined())) then k.effectTrigger.actionTrigger.thingWithFunction.getFunction.parameters->collect( p | thisModule.multipleParameters(p))
						else Sequence{} endif
	)
}

lazy rule multipleParameters {
	from s: String
	to 
	expression : ThingML!IntegerLiteral(
		intValue <- s.toInteger().refInvokeOperation('longValue', Sequence{}) -- bug : use of reflexivity to convert integer to long 
	)
}

lazy rule multipleTransition {
	from s: ThingML!Transition , k : CyprIoT!RuleTrigger
	to 
	performTransition : ThingML!Transition(
		target <- s.target,
		event <- receive
	),
	receive : ThingML!ReceiveMessage(
		port <- ThingML!Thing.allInstances().first().ports.first(),
		message <- thisModule.resolveTemp(k, 'triggerMessage'),
		name <- 'trigger'
	)
}

lazy rule groupActionOnEntry {
	from s : ThingML!Action , k : CyprIoT!RuleTrigger
	to 
	groupAction : ThingML!ActionBlock(
		actions <- if(not(s.oclIsUndefined())) then Sequence{action,s} else Sequence{action} endif
	),
	action : ThingML!SendAction(
		message <- thisModule.resolveTemp(k, 'triggerMessage'),
		port <- ThingML!Port.allInstances().first()
	)
	
}

