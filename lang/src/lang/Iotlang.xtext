grammar lang.Iotlang with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate iotlang "http://www.Iotlang.lang"

IoTLangModel returns IoTLangModel:
		(things+=Thing |policies+=Policy| messages+=Message | channels+=Channel | protocols += Protocol | configs+=NetworkConfiguration)*;
		
/* Terminals  */

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING_LIT	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"';
terminal ANNOTATION_ID:
	"@" ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

PlatformAnnotation returns PlatformAnnotation:
	name=ANNOTATION_ID value=STRING_LIT ;
	
/* Main types  */

Thing: // example gateway, arduino, android  
	'thing' name=ID
	( annotations+=PlatformAnnotation )*'{'
		 
	'}';

Channel: // mqtt, kafka, real simple bus,
	(PubSub|PointToPoint)
;
PubSub:
	'channel:pubsub' name=ID '{'
		(hasTopics +=Topic)*
		'}'
;

PointToPoint:
	'channel:ptp' name=ID '{'
		(hasTopics +=Topic)*
		'}'
;
Policy: // mqtt, kafka, real simple bus,
	'policy' name=ID '{'
		(hasRules +=Rule)*
	
'}'
;

Protocol :
	'protocol' name=ID
;

Message :
	'message' name=ID
;
Topic:
	'topic'  name=ID /*type=('?' |'!')*/ '(' acceptedMessages=[Message|ID] ')'
;

Rule:
	'rule' (name=ID)? (subject=[Thing|ID]) permission=('allow' |'deny') ':' action=('send'|'receive') object=[Thing|ID]
;


/* Configuration : Configure the WSN topology */ 

// Generate the topic description

Domain:
	'domain' name=STRING_LIT
;
InstanceThing:
	'instanceThing' name=ID (( '[' number+=INT ']')*)?':' (typeThing=[Thing|ID]) ( annotations+=PlatformAnnotation )*;

//instanceGateway:
//	'instanceGateway' name=ID (( '[' number+=INT ']')*)?':' (type=[Thing|ID]) ( annotations+=PlatformAnnotation )*;

InstanceChannel:
	'instanceChannel' name=ID (( '[' number+=INT ']')*)? ':' (typeChannel=[Channel|ID]) 'over' overProtocol=[Protocol|ID]( annotations+=PlatformAnnotation )*;

InstancePolicy:
	'instancePolicy' name=ID ':' (typePolicy=[Policy|ID]) ( annotations+=PlatformAnnotation )*;

NetworkConfiguration:
	'networkConfiguration' name=ID ( annotations+=PlatformAnnotation )* '{'
	(domain+=Domain | binds+=Bind| thingInstances+=InstanceThing | channelInstances+=InstanceChannel | 'enforce' enforces+=[InstancePolicy|ID] | instancePoliciy+=InstancePolicy)*
	
	'}';
Bind:
	'bind'	(name=ID)? thingInstance=[InstanceThing|ID] direction=('=>' |'<=>'|'<=') channelInstance=[InstanceChannel|ID]'{' topics+=[Topic|ID] (( "," topics+=[Topic|ID])*)?'}' ( annotations+=PlatformAnnotation )*;
