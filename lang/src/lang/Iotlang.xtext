grammar lang.Iotlang with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore



generate iotlang "http://www.Iotlang.lang"

IoTLangModel returns IoTLangModel:
		(imports+=Import | 
		types+=Type | 
		importThings+=Thing | 
		pubsubs+=PubSub |
		reqreps+=ReqRep |
		networks+=Network |
		users+=User |
		roles+=Role)*;
QualifiedName: ID ('.' ID)*
;
Import: 'import' importURI=STRING
;
QualifiedNameWithWildcard: QualifiedName '.*'?
;
Type:
DataType ;

DataType: 'datatype' name = ID
;

// Domain Model
Role:
	'role:' name=ID
;

User :
	'user:' name=ID 'assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*
;

Thing:
	'thing:' name=ID 
		'assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*
		'import' importPath=STRING_LIT
		ports+=[Port]
;

Property:
	'property' name=ID '=' propertyValue=STRING_LIT
;

Capacity:
	'property' name='capacity' '=' capacityValue=INT
;
Domain:
	'property' name='domain' '=' capacityValue=QualifiedNameWithWildcard
;

// Dynamicity of the configuration

Network:
	'network' name=ID '{'
		(networkProperty+=Property |
		instances+=AbstractInstance |
		binfs+=Bind
		)*
	'}'
;



AbstractInstance :
	(sink ?= 'sink')? 'instance'  typeInstances=Instance
;
Instance:
	InstanceThing | InstancePubSub | InstanceReqRep
;
InstanceThing :
	'thing:'typeThing=[Thing] '~' name=ID ('['numberOfInstances=INT']')?  ('owner' owner=[User])?
;
InstancePubSub :
	'pubsub:'typePubSub=[PubSub] '~' name=ID
;
InstanceReqRep :
	'reqrep:'typeReqRep=[ReqRep] '~' name=ID
;
ReqRep:
	'channel:reqrep' name=ID '{'
		(hasEndpoints +=Endpoint)*
	'}'
;
Port:
	name=ID
;
/*Stream:
	'channel:stream' name=ID '{'
		
	'}'
;*/
Group:
	'group' name=ID
;

Endpoint:
	'endpoint'  name=ID ('(' hasParameters+=ID ( "," assignedRoles+=ID)* ')')*
;

PubSub:
	'channel:pubsub' name=ID '{'
		(	chanProperties+=Property|
			hasTopics +=Topic
		)*
	'}'
;

Topic:
	'topic'  name=ID  ('subtopicOf' subtopicOf+=[Topic])?
;

Bind:
	'bind'	(name=ID)? thingInstance=[InstanceThing]"." subjectPort=ID readOrWrite=('=>' |'<=') bindsChannel=BindChannel
;
BindChannel :
	PubSubBind | ReqRepBind
;
PubSubBind :
	pubSubInstance=[InstancePubSub] '{' topics+=[Topic] ( "," topics+=[Topic])*'}'
;

ReqRepBind :
	reqRepInstance=[InstanceReqRep] '.' endpoints+=[Endpoint]
;
// Terminals

@Override 
terminal INT returns ecore::EInt: ('0'..'9')+;

terminal STRING_LIT	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"';

terminal ANNOTATION_ID:
	"@" ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;