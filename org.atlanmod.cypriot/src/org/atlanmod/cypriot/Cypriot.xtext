grammar org.atlanmod.cypriot.Cypriot with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.atlanmod.cypriot/model/ThingML.ecore" as thingml

generate cyprIoT "http://www.atlanmod.org/CyprIoT"

CyprIoTModel returns CyprIoTModel:
		imports+=Import*
		(declareThings+=Thing | 
		declareChannels+=Channel|
		specifyNetworks+=Network |
		declareUsers+=User |
		specifyPolicies+=Policy |
		declareRoles+=Role)*;

Import:
	'import' importURI=STRING 
;

NamedElement:
	User | Role | Thing | Network | Topic | PointToPoint | PubSub |
	InstanceThing | InstancePubSub | InstancePTP | ConnectionPoint | Topic |
	Bind |  Policy | RuleSubject | ToBindPTP | ToBindPubSub | Rule |
	";" name=ID
;

Role:
	'role' name=ID
;

User :
	'user' name=ID ('assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*)?
;

Thing:
	'thing' name=ID 
		('assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*)?
		'import' importPath=STRING
;

// Dynamicity of the configuration

PoliciesEnforcement :
	'enforce' policyName+=[Policy] ( "," policyName+=[Policy])* combiningAlgo=CombiningAlgo
;

enum CombiningAlgo:
	DENY='override-deny' | 
	ALLOW='override-allow'
;

Domain:
	'domain' name=DomainId
;
Network:
	'network' name=ID '{'
		domain=Domain
		(hasPolicyEnforcement=PoliciesEnforcement)?
		(instantiate+=Instance |
		hasBinds+=Bind |
		hasNetworkBridges+=NetworkBridge
		)*
	'}'
;

Instance:
	InstanceThing | InstancePubSub | InstancePTP
;

InstanceThing :
	'instance' name=ID  ('['declareNumberOfInstances=INT']')? ':' thingToInstantiate=[Thing] ('owner' owner=[User])? 'platform' targetedPlatform=Platform
;


InstancePubSub :
	'instance' name=ID ':' pubSubToInstantiate=[PubSub]   'platform' targetedPlatform=PubSubProtocol
;

InstancePTP :
	'instance' name=ID ':' ptPToInstantiate=[PointToPoint]    'platform'  targetedPlatform=PTPProtocol
;

Channel:
	PubSub | PointToPoint
;

PubSub:
	'channel:pubsub' name=ID '{'
		(hasTopics +=Topic)*
	'}'
;

Topic:
	'topic'  name=ID ('(' acceptedMessages+=ID ( "," assignedRoles+=ID)* ')')*  ('subtopicOf' subtopicOf+=[Topic])?
;

PointToPoint:
	'channel:ptp' name=ID '{'
		(hasConnectionPoints +=ConnectionPoint)*
	'}'
;

ConnectionPoint:
	'ConnectionPoint'  name=ID ('(' acceptedMessages+=ID ( "," assignedRoles+=ID)* ')')*
;

Bind:
	'bind'	(name=ID ':')? bindsInstanceThing=[InstanceThing]("." portToBind=[thingml::Port])? bindAction=BindAction channelToBind=ChannelToBind
;

enum Platform:
	POSIX='CPOSIX' | 
	JAVA='JAVA' |
	ARDUINO='ARDUINO' |
	JS='JS' |
	PYTHON='PYTHON'
;

enum PubSubProtocol:
	MQTT='MQTT' | 
	AMQP='AMQP' 
;

enum PTPProtocol:
	HTTP='HTTP' | 
	COAP='COAP' |
	UPNP='UPNP' |
	ZIGBEE='ZIGBEE' |
	ZWAVE='ZWAVE'
;

enum BindAction:
	READ='<=' |
	WRITE='=>'
;

ChannelToBind:
	ToBindPubSub | ToBindPTP
;

ToBindPubSub:
	 targetedPubSubInstance=[InstancePubSub] '{' topics+=[Topic] ( "," topics+=[Topic])*'}'
;

ToBindPTP:
	targetedPtpInstance=[InstancePTP] '.' (bindsToConnectionPoint=[ConnectionPoint] /* TODO | filterConnectionPointsUsingRegex=RegularExpression*/)
;

GlobalBridge:
	'bridge' name=ID '{'
		
	'}'
;

NetworkBridge:
	'bridge' bindSubject=[BridgeSubject] 'to' bridgeToChannel=ChannelToBind
;

BridgeSubject:
	Bind | Topic | ConnectionPoint
;

Policy:
	'policy' name=ID '{'
		(hasRules+=Rule)*
	'}'
;

Rule:
	'rule' (name=ID ':')? (ruleSubject=ThingSubject | roleSubject=[Role] | userSubject=[User]) ruleEffectAction=RuleEffectAction (ruleObject=RuleSubject | domainObject=DomainObject)  
		//(conditions=Conditions)?
;

ThingSubject:
	thingSubject=[ThingSubjectAny] ('->' ('port:' thingSubjectPort=[thingml::Port] | 'state:' thingSubjectState=[thingml::State])) ?
;

ThingSubjectAny:
	InstanceThing | Thing
;

RuleObject:
	Thing | Role | User | Instance | PubSub | PointToPoint | Topic | ConnectionPoint
;

/*Conditions:
	'when' conditionExpression=ConditionsExpression
;

ConditionsExpression: OrExpression;

OrExpression returns Expression:
	AndExpression ({OrExpression.lhs=current} "or" rhs=AndExpression)*
;

AndExpression returns Expression:
	ExpressionAttribute ({AndExpression.lhs=current} "and" rhs=ExpressionAttribute)*
;

ExpressionAttribute:
	attribute=[SubjectAndObject] '->' ((currentState?='currentState:' | nextState?='nextState:') subjectState=[thingml::State] | ( 'property:' subjectProperty=[thingml::Property] | 'message:' subjectMessage=[thingml::Message]'.'parameterMessage=[thingml::Parameter]) '=' subjectContentEqual=STRING)
;

SubjectAndObject:
	RuleSubject | RuleObject
;*/

DomainId:
	ValidID (=>'.' ValidID)+
;

DomainObject :
	DomainId  ('.' '*')?
;
	
RuleEffectAction :
	EffectComm |
	EffectTrigger |
	EffectBridge
;

EffectComm:
	(deny?='deny:' | allow?='allow:') actionComm=ActionComm
;

EffectBridge :
	bridge='bridge:' actionBridge=ActionBridge
;

EffectTrigger :
	trigger='trigger:' actionTrigger=ActionTrigger
;

enum ActionComm:
	send='send' | 
	receive='receive' |
	sendreceive='send-receive'
;

enum ActionBridge:
	from='from'| 
	to='to'
;

enum ActionTrigger:
	performTransition='performTransition' | 
	goToState='goToState' | 
	executeFunction='executeFunction'
;

/*
RegularExpression:
	OrRegularExpression
;

OrRegularExpression:
	SequenceExpression ({OrRegularExpression.left = current} '+' right=SequenceExpression)*
;

SequenceExpression:
	HighBindExpression ({SequenceExpression.left = current} right = HighBindExpression)*
;

HighBindExpression:
        AtomicRegularExpression ('*'{StarExpression.innerExpression=current} '*'* )?
;

AtomicRegularExpression:
	BracedExpression | Atom
;

BracedExpression:
	'(' innerExpression = RegularExpression ')'
;

Atom:
	value = CHARI
;

terminal CHARI returns ecore::EChar:
	('a'..'z'|'A'..'Z'|'0'..'9'|'\\0'|'*')
;

terminal TIME:
	
;

terminal LOCATION:
	
;*/


