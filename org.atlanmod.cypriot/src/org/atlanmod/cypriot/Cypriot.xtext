grammar org.atlanmod.cypriot.Cypriot with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/emf/2002/Ecore" as ecore



generate cyprIoT "http://www.atlanmod.org/CyprIoT"

CyprIoTModel returns CyprIoTModel:
		(types+=Type | 
		importThings+=Thing | 
		pubsubs+=PubSub |
		reqreps+=ReqRep |
		networks+=Network |
		users+=User |
		roles+=Role)*;

NamedElement:
	User | Role | Thing | Network | Topic |
	";" name=ID
;

Type:
DataType ;

DataType: 'datatype' name = ID
;

// Domain Model
Role:
	'role:' name=ID
;

User :
	'user:' name=ID 'assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*
;

Thing:
	'thing:' name=ID 
		'assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*
		'import' importPath=STRING_LIT
		//ports+=[Port]
;

Property:
	'property' name=ID '=' propertyValue=STRING_LIT
;

// Dynamicity of the configuration

Network:
	'network' name=ID '{'
		(networkProperty+=Property |
		instancesThing+=InstanceThing |
		instancesPubsub+=InstancePubSub |
		instancesReqrep+=InstanceReqRep |
		bindsPubsub+=BindPubSub | 
		bindReqRep+=BindReqRep
		)*
	'}'
;



InstanceThing :
	'instanceThing'typeThing=[Thing] '~' name=ID ('['numberOfInstances=INT']')?  ('owner' owner=[User])? platform=('@posix' | '@java' | '@arduino' | 'javascript')
;
InstancePubSub :
	'instancePubsub'typePubSub=[PubSub] '~' name=ID  platform=('@mqtt' | '@amqp' )
;
InstanceReqRep :
	'instanceReqrep'typeReqRep=[ReqRep] '~' name=ID platform=('@http' | '@coap' )
;
ReqRep:
	'channel:reqrep' name=ID '{'
		(hasEndpoints +=Endpoint)*
	'}'
;
Port:
	name=ID
;
/*Stream:
	'channel:stream' name=ID '{'
		
	'}'
;
Group:
	'group' name=ID
;*/

Endpoint:
	'endpoint'  name=ID ('(' hasParameters+=ID ( "," assignedRoles+=ID)* ')')*
;

PubSub:
	'channel:pubsub' name=ID '{'
		(	chanProperties+=Property|
			hasTopics +=Topic
		)*
	'}'
;

Topic:
	'topic'  name=ID  ('subtopicOf' subtopicOf+=[Topic])?
;

BindPubSub:
	'bindPubSub'	(name=ID)? thingInstance=[InstanceThing]"." subjectPort=ID readOrWrite=('=>' |'<=') pubSubInstance=[InstancePubSub] '{' topics+=[Topic] ( "," topics+=[Topic])*'}'
;

BindReqRep:
	'bindReqRep'	(name=ID)? thingInstance=[InstanceThing]"." subjectPort=ID readOrWrite=('=>' |'<=') reqRepInstance=[InstanceReqRep] '.' endpoint=[Endpoint]
;

// Terminals

@Override 
terminal INT returns ecore::EInt: ('1'..'9')('0'..'9')*;

terminal STRING_LIT	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"';

terminal ANNOTATION_ID:
	"@" ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*; 