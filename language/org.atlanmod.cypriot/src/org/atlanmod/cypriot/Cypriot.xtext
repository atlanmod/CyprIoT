grammar org.atlanmod.cypriot.Cypriot with org.eclipse.xtext.xbase.Xbase

generate cyprIoT "http://www.atlanmod.org/CyprIoT"
import "http://www.thingml.org/xtext/ThingML" as thingml

CyprIoTModel returns CyprIoTModel:
		imports+=Import*
		(
		declareTime+=Time |
		declareThings+=Thing | 
		declareChannels+=Channel|
		specifyNetworks+=Network |
		declareUsers+=User |
		specifyPolicies+=Policy |
		declareRoles+=Role)*;

Import:
	'import' importURI=STRING 
;

NamedElement:
	User | Role | Thing | Network | Topic | PointToPoint | PubSub |
	InstanceThing | InstancePubSub | InstancePTP | ConnectionPoint | Topic |
	Bind |  Policy | ToBindPTP | ToBindPubSub | Rule |
	";" name=ID
;

Role:
	'role' name=ID
;

User :
	'user' name=ID ('assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*)?
;

Thing:
	'thing' name=ID 
		('assigned' assignedRoles+=[Role] ( "," assignedRoles+=[Role])*)?
		'import' importPath=STRING
;

// TODO Dynamicity of the configuration

PoliciesEnforcement :
	'enforce' policyName+=[Policy] ( "," policyName+=[Policy])* combiningAlgo=CombiningAlgo
;

enum CombiningAlgo:
	DENY='override-deny' | 
	ALLOW='override-allow'
;

Domain:
	'domain' name=DomainId
;
Network:
	'network' name=ID '{'
		domain=Domain
		(hasPolicyEnforcement=PoliciesEnforcement)?
		(instantiate+=Instance |
		hasBinds+=Bind |
		hasNetworkBridges+=NetworkBridge
		)*
	'}'
;

Instance:
	InstanceThing | InstanceChannel
;

InstanceThing :
	'instance' name=ID  ('['declareNumberOfInstances=INT']')? ':' typeThing=TypeThing
;

InstanceChannel:
	InstancePubSub | InstancePTP
;

InstancePubSub :
	'instance' name=ID ':' typePubSub=TypePubSub
;

InstancePTP :
	'instance' name=ID ':' typePTP=TypePTP
;

TypeThing :
	thingToInstantiate=[Thing] 'platform' targetedPlatform=Platform ('owner' owner=[User])?
;

TypePubSub :
	pubSubToInstantiate=[PubSub] 'protocol' targetedProtocol=PubSubProtocol
;

TypePTP :
	ptPToInstantiate=[PointToPoint] 'protocol'  targetedProtocol=PTPProtocol
;

Channel:
	PubSub | PointToPoint
;

PubSub:
	'channel:pubsub' name=ID '{'
		(hasTopics+=Topic)*
	'}'
;

Topic:
	'topic'  name=ID ('=' topicName=STRING)? ('(' acceptedMessages+=ID ( "," assignedRoles+=ID)* ')')*  ('subtopicOf' subtopicOf+=[Topic])?
;

PointToPoint:
	'channel:ptp' name=ID '{'
		(hasConnectionPoints+=ConnectionPoint)*
	'}'
;

ConnectionPoint:
	'ConnectionPoint'  name=ID ('(' acceptedMessages+=ID ( "," assignedRoles+=ID)* ')')*
;

Bind:
	'bind'	(name=ID ':')? bindsInstanceThing=[InstanceThing]("." portToBind=[thingml::Port])? bindAction=BindAction channelToBind=ChannelToBind
;

enum Platform:
	POSIX='CPOSIX' | 
	JAVA='JAVA' |
	ARDUINO='ARDUINO' |
	JS='JS' |
	PYTHON='PYTHON'
;

enum PubSubProtocol:
	MQTT='MQTT' | 
	AMQP='AMQP' 
;

enum PTPProtocol:
	HTTP='HTTP' | 
	COAP='COAP' |
	UPNP='UPNP' |
	ZIGBEE='ZIGBEE' |
	ZWAVE='ZWAVE'
;

enum BindAction:
	READ='<=' |
	WRITE='=>'
;

ChannelToBind:
	ToBindPubSub | ToBindPTP
;

ToBindPubSub:
	 targetedPubSubInstance=[InstancePubSub] '{' topics+=[Topic] ( "," topics+=[Topic])*'}'
;

ToBindPTP:
	targetedPtpInstance=[InstancePTP] '.' (bindsToConnectionPoint=[ConnectionPoint] /* TODO | filterConnectionPointsUsingRegex=RegularExpression*/)
;

BridgeSubject:
	Bind | Topic | ConnectionPoint
;

Policy:
	'policy' name=ID '{'
		(hasRules+=Rule)*
	'}'
;

Rule:
	(RuleComm | RuleTrigger | RuleBridge) /*(conditions=Conditions)?*/

;

RuleComm:
	'rule' (name=ID ':')? commSubject=CommSubjectObject effectComm=EffectComm commObject=CommSubjectObject
;

RuleTrigger:
	'rule' (name=ID ':')? thingWithState=ThingWithState effectTrigger=EffectTrigger
;

RuleBridge:
	'rule' (name=ID ':')? bridgeSubject=BridgeSubjectObject effectBridge=EffectBridge bridgeObject=BridgeSubjectObject
;

BridgeSubjectObject:
	 PubSubWithTopic | PTPWithConnectionPoint | ThingWithPort
;

CommSubjectObject :
	thingWithStateOrPort=ThingWithStateOrPort | subjectOther=[SubjectObjectOther]
;

ThingAny:
	InstanceThing | Thing
;

SubjectObjectOther:
	Role | User | ThingAny
;

ThingWithStateOrPort:
	ThingWithPort | ThingWithState
;

PTPWithConnectionPoint:
	ptp=[PointToPoint] '->' getConnectionPoint=GetConnectionPoint
;

PubSubWithTopic:
	pubsub=[PubSub] '->' getTopic=GetTopic
;

ThingWithPort:
	thing=[ThingAny] '->' getPort=GetPort
;

ThingWithState:
	thing=[ThingAny] '->' getState=GetState
;

ThingWithFunction:
	thing=[ThingAny] '->' getFunction=GetFunction
;

GetConnectionPoint:
	'connectionPoint:' connectionPoint=[ConnectionPoint]
;

GetTopic:
	'topic:' topic=[Topic]
;

GetPort:
	'port:' port=[thingml::Port]
;

GetState:
	'state:' state=[thingml::State]
;

GetFunction:
	'function:' function=[thingml::Function]
;

EffectComm:
	(deny?='deny:' | allow?='allow:') actionComm=ActionComm
;

EffectTrigger:
	'trigger:' actionTrigger=ActionTrigger
;

EffectBridge:
	'bridge:' actionBridge=ActionBridge
;

ActionTrigger:
	(performTransition='performTransition' thingToTransition=[ThingAny]) | 
	(goToState='goToState' thingWithState=ThingWithState) | 
	(executeFunction='executeFunction' thingWithFunction=ThingWithFunction)
;

enum ActionComm:
	send='send' | 
	receive='receive' |
	sendreceive='send-receive'
;

enum ActionBridge:
	from='from'| 
	to='to'
;

Conditions:
	'when' conditionExpression=ConditionsExpression
;

ConditionsExpression: OrExpression;

OrExpression returns Expression:
	AndExpression ({OrExpression.lhs=current} "or" rhs=AndExpression)*
;

AndExpression returns Expression:
	ExpressionAttribute ({AndExpression.lhs=current} "and" rhs=ExpressionAttribute)*
;

ExpressionAttribute:
	attribute=[ThingAny] '->' ((currentState?='currentState:' | nextState?='nextState:') subjectState=[thingml::State] | ( 'property:' subjectProperty=[thingml::Property] | 'message:' subjectMessage=[thingml::Message]'.'parameterMessage=[thingml::Parameter]) '=' subjectContentEqual=STRING)
;

DomainId:
	ValidID (=>'.' ValidID)+
;

NetworkBridge:
	'bridge' (name=ID ':')? bridgeSubject=[BridgeSubject] actionBridge=ActionBridge bridgeToChannel=ChannelToBind
;

Time: // Cron syntax
	'time' name=ID cronExpression=CronExpression
;

CronExpression:
	seconds=CronElement
	minutes=CronElement
	hours=CronElement
	days=CronElement
	months=CronElement
	daysOfWeek=CronElement
	(year=CronElement)?
	| '@' constant=ID
;

CronElement:
	RangeCronElement | PeriodicCronElement
;

RangeCronElement hidden():
	TerminalCronElement ({RangeCronElement.start=current} '-' end=IntLiteral)*
;

TerminalCronElement:
	expression=(IntLiteral | ID | '*' | '?')
;

PeriodicCronElement hidden():
	expression=TerminalCronElement '/' elements=RangeCronList
;

RangeCronList hidden():
	elements+=RangeCronElement (',' elements+=RangeCronElement)*
;

IntLiteral:
	INT
;
